/*Gaussian Distribution Example
* creates a dandelion using gaussian 
* distribution of points.
*/

/*number of seeds in the dandelion. */
num = 150 

/*x center of dandelion flower. */
centerX = WIDTH/2

/*y center of dandelion flower. */
centerY = HEIGHT/2

/*radius of hole in the center of the flower */
centerRad = 65

/* group to hold seeds in flower */
seeds= group()

/* start loop to create seeds*/
repeat i=0:num:
	/*calculate x and y position of each seed
	* using gaussianNoise function
	* gaussianNoise(mu,sigma)
	* mu: the center of the distribution
	* sigma: the width of the distribution
	*/
	x = gaussianNoise(centerX, 100)
	y = gaussianNoise(centerY, 100)
	
	/*create a new seed using the seed stamp and move it 
	* to the x and y coordinates generated with 
	* the gaussian noise
	*/
	s = seed()
	moveTo(s,x,y)
	
	/* rotate the seed so that it faces the center
	* of the dandelion and move it away from the center 
	* by the distance defined in center rad
	*/
	deltaY = centerY-getY(s)
	deltaX  = centerX-getX(s) 
	
	theta = getAngle(deltaY,deltaX)
	headingBy(s,360-theta,centerRad)
	
	deltaY = centerY-getY(s)
	deltaX  = centerX-getX(s)
	
	theta = getAngle(deltaY,deltaX) 
	rotate(s,360-theta)
	
	/* scale the seed by how far it is away
	* from the center of the flower
	*/
	rad = 250-getRadius(deltaY,deltaX)
	rad = map(rad,0,300,0.5,1.5)
	scale(s,rad,rad)
	
	/*add the seed to the seed group*/
	add(seeds,s)
	
/*end of loop*/

/*create the stem from a curve*/
stem = curve(316.00,412.00,542.00,702.00,556.00,810.00,416.00,1184.00)
weight(stem,3)

/*create a group containing the seeds and the 
* stem and position it and scale it down
*/
flower = group(seeds,stem)
scale(flower,0.75,0.75)
moveTo(flower,313.3,339.3)







